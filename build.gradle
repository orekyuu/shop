plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE' apply false
    id "com.github.jlouns.cpe" version "0.5.0"
}

group 'net.orekyuu'
version '1.0-SNAPSHOT'

repositories {
    jcenter()
}


subprojects {
    apply plugin: 'java'
    sourceCompatibility = 12

    def defaultEncoding = 'UTF-8'
    [AbstractCompile, Javadoc].each {
        tasks.withType(it).each { it.options.encoding = defaultEncoding }
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        testImplementation platform('org.junit:junit-bom:5.5.0')

        testImplementation 'org.junit.jupiter:junit-jupiter-api'
        testImplementation 'org.junit.platform:junit-platform-commons'
        testImplementation 'org.junit.jupiter:junit-jupiter-params'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
        testImplementation 'org.assertj:assertj-core:3.12.2'
        testImplementation 'org.mockito:mockito-core:+'
    }

    test {
        useJUnitPlatform {
            includeEngines 'junit-jupiter'
        }
    }
}

project("core") {
    dependencies {
        implementation project(":identity")
    }
}

project("web") {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    dependencies {
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.1.6.RELEASE')

        implementation 'org.springframework.boot:spring-boot-starter-actuator'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-security'
        implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'

        implementation project(':core')

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit'
        }
    }

    task buildWebUiResources(type: CrossPlatformExec) {
        workingDir '../web-ui'
        commandLine 'yarn', 'run', 'build'
    }

    task copyWebUiResources(type: Copy, dependsOn: ':web:buildWebUiResources') {
        from '../web-ui/build'
        into "${sourceSets.main.output.resourcesDir}/static/assets"
    }

    processResources {
        // web-uiで作った青果物をコピーしてくる
        dependsOn copyWebUiResources
    }
}
